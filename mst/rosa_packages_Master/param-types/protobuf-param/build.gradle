apply plugin: 'com.android.library'

android {
    def sdk = rootProject.ext.sdk
    compileSdkVersion sdk.compileSdkVersion

    defaultConfig {
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api project(':master-sdk')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def deps = rootProject.ext.deps
    implementation deps.appcompat
    api deps.protobuf

    testImplementation deps.junit
    androidTestImplementation deps.espressoCore
    androidTestImplementation deps.runner
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

task jarLib(
        type: Jar,
        dependsOn: [':param-types:protobuf-param:assembleRelease']) {
    archiveName = "protobuf-param.jar"

    exclude('**/BuildConfig.class')

    from(project.zipTree("build/intermediates/bundles/release/classes.jar"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.ubtrobot.master'
            artifactId 'protobuf-param'
            version rootProject.ext.versions.protobufParamVersion

            artifact("build/libs/protobuf-param.jar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.ubtrobot.master')
                dependencyNode.appendNode('artifactId', 'master')
                dependencyNode.appendNode('version', rootProject.ext.versions.sdkVersion)

                dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'com.google.protobuf')
                dependencyNode.appendNode('artifactId', 'protobuf-java')
                dependencyNode.appendNode('version', rootProject.ext.versions.protobufVersion)
            }

        }
    }
}

artifactory {
    contextUrl = artifactory_url

    publish {
        repository {
            username = artifactory_username
            password = artifactory_password

            if (rootProject.ext.versions.sdkVersion.toUpperCase().endsWith("-SNAPSHOT")) {
                repoKey = artifactory_rose_snapshot
            } else {
                repoKey = artifactory_rose_release
            }
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }

    resolve {
        repoKey = 'jcenter'
    }
}
